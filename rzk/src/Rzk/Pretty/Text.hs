{-# OPTIONS_GHC -fno-warn-orphans #-}
{-# LANGUAGE FlexibleInstances #-}
{-# LANGUAGE LambdaCase        #-}
{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE RecordWildCards   #-}
module Rzk.Pretty.Text where

import           Data.Char           (chr, ord)
import           Data.Monoid         (Endo (..))
import           Data.Text           (Text)
import qualified Data.Text           as Text
import qualified Data.Text.IO        as Text
import           System.Console.ANSI
import           Text.Read           (readMaybe)

import           Rzk.Syntax.Decl
import           Rzk.Syntax.Module
import           Rzk.Syntax.Term
import           Rzk.Syntax.Var

-- Orphan 'Show' instances

instance Show (Term Var) where
  show = Text.unpack . ppTerm

instance Show (Decl Var) where
  show = Text.unpack . ppDecl

instance Show (Module Var) where
  show = Text.unpack . ppModule

-- * Unicode pretty-printers

-- | Pretty-print a variable.
ppVar :: Var -> Text
ppVar = colored [SetColor Foreground Vivid Blue] . identToUnicode . getVar

-- | Pretty-print a variable.
ppHole :: Var -> Text
ppHole = ("?" <>) . identToUnicode . getVar

ppReserved :: Text -> Text
ppReserved = colored [SetColor Foreground Vivid Yellow]

ppElimWithArgs :: Text -> [Term Var] -> Text
ppElimWithArgs name args = name <> "(" <> Text.intercalate ", " (map ppTerm args) <> ")"

-- | Pretty-print a 'Term' with default type of variables.
ppTerm :: Term Var -> Text
ppTerm = \case
  Variable x -> ppVar x
  TypedTerm term ty -> ppTermParen term <> " : " <> ppTerm ty
  Hole x -> ppHole x
  Universe   -> ppReserved "ùí∞"
  Pi (Lambda x (Just a) Nothing m) ->
    "(" <> ppVar x <> " : " <> ppTerm a <> ") ‚Üí " <> ppTerm m
  Pi (Lambda x (Just a) (Just phi) m) ->
    "{" <> ppVar x <> " : " <> ppTerm a <> " | " <> ppTerm phi <> "} ‚Üí " <> ppTerm m
  Pi t -> "Pi " <> ppTermParen t
  Lambda x (Just a) Nothing m
    -> "Œª(" <> ppVar x <> " : " <> ppTerm a <> ") ‚Üí " <> ppTerm m
  Lambda x Nothing Nothing m
    -> "Œª" <> ppVar x <> " ‚Üí " <> ppTerm m
  Lambda x (Just a) (Just phi) m
    -> "Œª{" <> ppVar x <> " : " <> ppTerm a <> " | " <> ppTerm phi <> "} ‚Üí " <> ppTerm m
  Lambda x Nothing (Just phi) m
    -> "Œª{" <> ppVar x <> " | " <> ppTerm phi <> "} ‚Üí " <> ppTerm m
  App t1 t2 -> ppTermParen t1 <> " " <> ppTermParen t2

  Sigma (Lambda x (Just a) Nothing m) -> "‚àë (" <> ppVar x <> " : " <> ppTerm a <> "), " <> ppTerm m
  Sigma t -> "‚àë" <> ppTermParen t
  Pair t1 t2 -> "(" <> ppTerm t1 <> ", " <> ppTerm t2 <> ")"
  First t -> ppReserved "œÄ‚ÇÅ " <> ppTermParen t
  Second t -> ppReserved "œÄ‚ÇÇ " <> ppTermParen t

  IdType a x y -> ppTermParen x <> " =_{" <> ppTerm a <> "} " <> ppTermParen y
  Refl (Just a) x -> ppReserved "refl" <> "_{" <> ppTerm x <> " : " <> ppTerm a <> "}"
  Refl Nothing x -> ppReserved "refl" <> "_{" <> ppTerm x <> "}"
  IdJ tA a tC d x p -> ppElimWithArgs (ppReserved "idJ") [tA, a, tC, d, x, p]

  Cube -> ppReserved "CUBE"
  CubeUnit -> ppReserved "ùüô"
  CubeUnitStar -> ppReserved "‚ãÜ"
  CubeProd i j -> ppTermParen i <> " √ó " <> ppTermParen j

  Tope -> ppReserved "TOPE"
  TopeTop -> ppReserved "‚ä§"
  TopeBottom -> ppReserved "‚ä•"
  TopeOr psi phi -> ppTermParen psi <> " ‚à® " <> ppTermParen phi
  TopeAnd psi phi -> ppTermParen psi <> " ‚àß " <> ppTermParen phi
  TopeEQ x y -> ppTermParen x <> " ‚â° " <> ppTermParen y

  RecBottom -> ppReserved "rec‚ä•"
  RecOr psi phi a_psi a_phi -> ppElimWithArgs (ppReserved "rec‚à®") [psi, phi, a_psi, a_phi]

  ExtensionType t cI psi tA phi a ->
    "„Äà{" <> ppVar t <> " : " <> ppTerm cI <> " | " <> ppTerm psi <> "} ‚Üí " <> ppTerm tA <> "[ " <> ppTermParen phi <> " ‚Ü¶ " <> ppTerm a <> " ]„Äâ"

  Cube2 -> ppReserved "ùüö"
  Cube2_0 -> ppReserved "0"
  Cube2_1 -> ppReserved "1"
  TopeLEQ t s -> ppTermParen t <> " " <> ppReserved "‚â§" <> " " <> ppTermParen s

  where
    ppTermParen t@(Variable _) = ppTerm t
    ppTermParen t@(Hole     _) = ppTerm t
    ppTermParen t@Universe     = ppTerm t
    ppTermParen t@Cube         = ppTerm t
    ppTermParen t@CubeUnit     = ppTerm t
    ppTermParen t@CubeUnitStar = ppTerm t
    ppTermParen t@Tope         = ppTerm t
    ppTermParen t@TopeTop      = ppTerm t
    ppTermParen t@TopeBottom   = ppTerm t
    ppTermParen t@RecBottom    = ppTerm t
    ppTermParen t@Cube2        = ppTerm t
    ppTermParen t@Cube2_0      = ppTerm t
    ppTermParen t@Cube2_1      = ppTerm t
    ppTermParen t              = "(" <> ppTerm t <> ")"

ppDecl :: Decl Var -> Text
ppDecl Decl{..} = Text.intercalate "\n"
  [ ppVar declName <> " : " <> ppTerm declType
  , "  := " <> ppTerm declBody
  ]

ppModule :: Module Var -> Text
ppModule Module{..} = Text.intercalate "\n\n"
  (map ppDecl moduleDecls)

-- * ASCII pretty-printers

-- | Pretty-print a variable in ASCII.
ppVarASCII :: Var -> Text
ppVarASCII = unicodeToAscii . getVar

-- | Pretty-print a 'Term', but without fancy Unicode symbols.
ppTermASCII :: Term Var -> Text
ppTermASCII = unicodeToAscii . ppTerm

-- * Unicode and ASCII helpers

-- | Convert an identifier with number at the end into an identifier with subscript number:
--
-- >>> Var (identToUnicode "x3")
-- x‚ÇÉ
identToUnicode :: Text -> Text
identToUnicode s =
  case readMaybe oldIndex of
    Nothing        -> s
    Just oldIndexN -> prefix <> newIndex (oldIndexN :: Integer)
  where
    (prefix, index) = Text.break isDigitOrDigitSub s

    digits    = "0123456789" :: String
    digitsSub = "‚ÇÄ‚ÇÅ‚ÇÇ‚ÇÉ‚ÇÑ‚ÇÖ‚ÇÜ‚Çá‚Çà‚Çâ" :: String
    isDigitSub = (`elem` digitsSub)
    isDigit    = (`elem` digits)
    isDigitOrDigitSub c = isDigit c || isDigitSub c
    digitFromSub c
      | isDigitSub c = chr ((ord c - ord '‚ÇÄ') + ord '0')
      | otherwise    = c
    digitToSub c = chr ((ord c - ord '0') + ord '‚ÇÄ')

    oldIndex  = '0' : map digitFromSub (Text.unpack index)
    newIndex oldIndexN
      | Text.null index = ""
      | otherwise       = Text.pack (digitToSub <$> show oldIndexN)


-- | Convert Unicode representation into ASCII by replacing
-- known Unicode symbols by their ASCII analogues.
--
-- >>> unicodeToAscii "Œª (A : ùí∞) ‚Üí B"
-- "\\ (A : U) -> B"
unicodeToAscii :: Text -> Text
unicodeToAscii = appEndo (foldMap (Endo . uncurry Text.replace) knownAsciiReplacements)

-- | Known Unicode symbols and their ASCII replacements:
--
-- >>> ppReplacements knownAsciiReplacements
-- ùí∞ U
-- Œª \
-- ‚Üí ->
-- ‚àè Pi
-- ‚ÇÄ 0
-- ‚ÇÅ 1
-- ‚ÇÇ 2
-- ‚ÇÉ 3
-- ‚ÇÑ 4
-- ‚ÇÖ 5
-- ‚ÇÜ 6
-- ‚Çá 7
-- ‚Çà 8
-- ‚Çâ 9
knownAsciiReplacements :: [(Text, Text)]
knownAsciiReplacements =
  [ ("ùí∞", "U")
  , ("Œª", "\\")
  , ("‚Üí", "->")
  , ("‚àè", "Pi")
  ] <> zipWith mkCharReplacement digitsSub digits
  where
    mkCharReplacement from to = (Text.pack [from], Text.pack [to])
    digitsSub = "‚ÇÄ‚ÇÅ‚ÇÇ‚ÇÉ‚ÇÑ‚ÇÖ‚ÇÜ‚Çá‚Çà‚Çâ"
    digits    = "0123456789"

-- | Pretty-print replacements. See 'knownAsciiReplacements'.
ppReplacements :: [(Text, Text)] -> IO ()
ppReplacements = mapM_ (\(from, to) -> Text.putStrLn (from <> " " <> to))

indent :: [Text] -> [Text]
indent = map ("  " <>)

-- ** Colors

colored :: [SGR] -> Text -> Text
colored sgrs t = Text.pack prefix <> t <> Text.pack suffix
  where
    prefix = setSGRCode sgrs
    suffix = setSGRCode [Reset]
