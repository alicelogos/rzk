# Formalizing proofs from 2017 paper by Riehl and Shulman

Here we present and explain formalization of definitions, propositions, and proofs from Riehl and Shulman's «Type Theory for Synthetic ∞-categories» ([https://arxiv.org/abs/1705.07442](https://arxiv.org/abs/1705.07442)).

## Simplicial Type Theory

This follows definitions and proofs from RS17, Section 3.

### Simplicies and their subshapes

First, we define n-simplices for small n:

```rzk
#def Δ¹ : (t : 2) -> TOPE
  := \(t : 2) -> TOP

#def Δ² : (t : 2 * 2) -> TOPE
  := \(t, s) -> s <= t

#def Δ³ : (t : 2 * 2 * 2) -> TOPE
  := \((t1, t2), t3) -> t3 <= t2 /\ t2 <= t1
```

Next, we define boundaries of n-simplices:

```rzk
#def ∂Δ¹ : (t : 2) -> TOPE
  := \(t : 2) -> 0_2 === t \/ 1_2 === t

#def ∂Δ² : (t : 2 * 2) -> TOPE
  := \(t, s) -> (0_2 === s /\ s <= t) \/ (s === t) \/ (s <= t /\ t === 1_2)
```

### Connection Squares (Proposition 3.5)

```rzk
#def V : (A : U) ->
         (f : <{t : 2 | TOP} -> A[BOT |-> recBOT]>) ->
         <{ts : 2 * 2 | TOP} -> A[BOT |-> recBOT]>
  := \A -> \f ->
     \(t, s) -> recOR(t <= s, s <= t, f s, f t)
```

```rzk
#def Λ : (A : U) ->
         (f : <{t : 2 | TOP} -> A[BOT |-> recBOT]>) ->
         <{ts : 2 * 2 | TOP} -> A[BOT |-> recBOT]>
  := \A -> \f ->
     \(t, s) -> recOR(t <= s, s <= t, f t, f s)
```

### Shape products

For Propositions 3.6 and 3.7, we need to introduce product shapes.
First, let's define product of two topes in general:

```rzk
#def shapeProd : (I : CUBE) -> (J : CUBE) ->
                 (psi : (t : I) -> TOPE) ->
                 (chi : (s : J) -> TOPE) ->
                 (ts : I * J) -> TOPE
  := \I -> \J -> \psi -> \chi ->
     \(t, s) -> psi t /\ chi s
```

Now, we can define the shapes used in Propositions 3.6 and 3.7:

```rzk
#def Δ¹×Δ¹ : (t : 2 * 2) -> TOPE
  := shapeProd 2 2 Δ¹ Δ¹

Δ²×Δ¹ : (t : 2 * 2 * 2) -> TOPE
  := shapeProd (2 * 2) 2 Δ² Δ¹
```


## RS17, Section 4

### Prerequisites

```rzk
prod : (A : U) -> (B : U) -> U
  := \A -> \B -> ∑ (x : A), B

isweq : (A : U) -> (B : U) -> (f : (_ : A) -> B) -> U
  := \A -> \B -> \f -> ∑ (g : (_ : B) -> A), prod ((x : A) -> g (f x) =_{A} x) ((y : B) -> f (g y) =_{B} y)

weq : (A : U) -> (B : U) -> U
  := \A -> \B -> ∑ (f : (_ : A) -> B), isweq A B f
```

### Theorem 4.1 (commutation of Pi and extension types)

```rzk
Theorem-4.1 : (I : CUBE) -> (psi : (t : I) -> TOPE) -> (phi : {(t : I) | psi t} -> TOPE) -> (X : U) -> (Y : <{t : I | psi t} -> (x : X) -> U >) -> (f : <{t : I | phi t} -> (x : X) -> Y t x >) -> weq <{t : I | psi t} -> (x : X) -> Y t x [phi t |-> f t]> ((x : X) -> <{t : I | psi t} -> Y t x [phi t |-> f t x]>)
  := \I -> \psi -> \phi -> \X -> \Y -> \f -> (\k -> \x -> \t -> k t x, (\k -> \{t : I | psi t} -> \x -> (k x) t, (\k -> refl_{k}, \k -> refl_{k})))
```

### Theorem 4.2 (currying and commutation of arguments for extension types)

```rzk
uncurry_ext : (I : CUBE) -> (J : CUBE) -> (psi : (t : I) -> TOPE) -> (zeta : (s : J) -> TOPE) -> (X : <{t : I | psi t} -> <{s : J | zeta s} -> U> >) -> (chi : {(t : I) | psi t} -> TOPE) -> (phi : {(s : J) | zeta s} -> TOPE) -> (f : <{(t, s) : I * J | psi t /\ zeta s} -> X t s >) -> (_ : <{t : I | psi t} -> <{s : J | zeta s} -> X t s [chi s |-> f (t, s)]> [phi t |-> \s -> f (t, s)]>) -> <{(t, s) : I * J | psi t /\ zeta s} -> X t s [(phi t /\ zeta s) \/ (psi t /\ chi s) |-> f (t, s)]>
  := \I -> \J -> \psi -> \zeta -> \X -> \chi -> \phi -> \f -> \k -> \(t, s) -> k t s
```

