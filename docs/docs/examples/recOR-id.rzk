#lang rzk-1

#def iscontr : (A : U) -> U
  := \A -> ∑ (a : A), (x : A) -> a =_{A} x

#def prod : (A : U) -> (B : U) -> U
  := \A -> \B -> ∑ (x : A), B

#def isweq : (A : U) -> (B : U) -> (f : (x : A) -> B) -> U
  := \A -> \B -> \f -> ∑ (g : (x : B) -> A), prod ((x : A) -> g (f x) =_{A} x) ((y : B) -> f (g y) =_{B} y)

#def weq : (A : U) -> (B : U) -> U
  := \A -> \B -> ∑ (f : (x : A) -> B), isweq A B f

#def transport
  : (A : U) ->
    (C : (x : A) -> U) ->
    (x : A) ->
    (y : A) ->
    (p : x =_{A} y) ->
    (cx : C x) ->
    C y
  := \A -> \C -> \x -> \y -> \p -> \cx -> idJ(A, x, (\z -> \q -> C z), cx, y, p)

#def relfunext : U
  :=
    (I : CUBE) ->
    (psi : (t : I) -> TOPE) ->
    (phi : (t : I) -> TOPE) ->
    (A : <{t : I | psi t} -> U >) ->
    (iscontrA : <{t : I | psi t} -> iscontr (A t) >) ->
    (a : <{t : I | psi t /\ phi t} -> A t >) ->
    <{t : I | psi t} -> A t [ psi t /\ phi t |-> a t]>

#def relfunext2 : U
  :=
    (I : CUBE) ->
    (psi : (t : I) -> TOPE) ->
    (phi : (t : I) -> TOPE) ->
    (A : <{t : I | psi t} -> U >) ->
    (a : <{t : I | psi t /\ phi t} -> A t >) ->
    (f : <{t : I | psi t} -> A t [ psi t /\ phi t |-> a t ]>) ->
    (g : <{t : I | psi t} -> A t [ psi t /\ phi t |-> a t ]>) ->
    weq (f =_{<{t : I | psi t} -> A t [ psi t /\ phi t |-> a t ]>} g)
      <{t : I | psi t} -> f t =_{A t} g t [ psi t /\ phi t |-> refl_{f t} ]>

#def restrict
  : (I : CUBE) ->
    (psi : (t : I) -> TOPE) ->
    (phi : (t : I) -> TOPE) ->
    (A : (t : I) -> U) ->
    (a : <{t : I | psi t} -> A t >) ->
    <{t : I | psi t /\ phi t} -> A t >
  := \I -> \psi -> \phi -> \A -> \a ->
     \t -> a t

#def ext-of-restrict
  : (I : CUBE) ->
    (psi : (t : I) -> TOPE) ->
    (phi : (t : I) -> TOPE) ->
    (A : (t : I) -> U) ->
    (a : <{t : I | psi t} -> A t >) ->
    <{t : I | psi t} -> A t [ psi t /\ phi t |-> restrict I psi phi A a t ]>
  := \I -> \psi -> \phi -> \A -> \a ->
     \t -> a t

#def restricts-path
  : (r : relfunext2) ->
    (I : CUBE) ->
    (psi : (t : I) -> TOPE) ->
    (phi : (t : I) -> TOPE) ->
    (A : (t : I) -> U) ->
    (a_psi : <{t : I | psi t} -> A t >) ->
    (a_phi : <{t : I | phi t} -> A t >) ->
    (e : <{t : I | psi t /\ phi t} -> a_psi t =_{A t} a_phi t >) ->
    restrict I psi phi A a_psi
      =_{ <{t : I | psi t /\ phi t} -> A t > }
    restrict I phi psi A a_phi
  :=
    \r ->
    \I -> \psi -> \phi -> \A -> \a_psi -> \a_phi -> \e ->
    (first (second (r I
      (\t -> psi t /\ phi t)
      (\t -> BOT)
      (\t -> A t)
      (\t -> recBOT)
      (\t -> a_psi t)
      (\t -> a_phi t)))) e

#def recOR-id
  : (r : relfunext2) ->
    (I : CUBE) ->
    (psi : (t : I) -> TOPE) ->
    (phi : (t : I) -> TOPE) ->
    (A : (t : I) -> U) ->
    (a_psi : <{t : I | psi t} -> A t >) ->
    (a_phi : <{t : I | phi t} -> A t >) ->
    (e : <{t : I | psi t /\ phi t} -> a_psi t =_{A t} a_phi t >) ->
    <{t : I | psi t \/ phi t} -> A t >
  := \r ->
     \I -> \psi -> \phi -> \A -> \a_psi -> \a_phi -> \e ->
     \t -> recOR(psi t, phi t
       , transport
            <{t : I | psi t /\ phi t} -> A t >
            (\ra -> <{t : I | psi t} -> A t [ psi t /\ phi t |-> ra t]>)
            (restrict I psi phi A a_psi)
            (restrict I phi psi A a_phi)
            (restricts-path r I psi phi A a_psi a_phi e)
            (ext-of-restrict I psi phi A a_psi)
            t
       , ext-of-restrict I phi psi A a_phi t)

-- ∑ (g₁ :
--     (x : 〈{t : I | (psi t) ∧ (phi t)} → (a_psi t) =_{A t} (a_phi t)[ (((psi t) ∧ (phi t)) ∧ ⊥) ↦ refl_{rec⊥} ]〉)
--     → (λt → a_psi t) =_{〈{t : I | (psi t) ∧ (phi t)} → A t[ (((psi t) ∧ (phi t)) ∧ ⊥) ↦ rec⊥ ]〉} (λt → a_phi t))
--       ,
--   ∑ (x : (x : (λt → a_psi t) =_{〈{t : I | (psi t) ∧ (phi t)} → A t[ (((psi t) ∧ (phi t)) ∧ ⊥) ↦ rec⊥ ]〉} (λt → a_phi t)) → (g₁ ((π₁ (((((((r I) (λt → (psi t) ∧ (phi t))) (λt → ⊥)) (λt → A t)) (λt → rec⊥)) (λt → a_psi t)) (λt → a_phi t))) x)) =_{(λt → a_psi t) =_{〈{t : I | (psi t) ∧ (phi t)} → A t[ (((psi t) ∧ (phi t)) ∧ ⊥) ↦ rec⊥ ]〉} (λt → a_phi t)} x),
--     (y : 〈{t : I | (psi t) ∧ (phi t)} → (a_psi t) =_{A t} (a_phi t)[ (((psi t) ∧ (phi t)) ∧ ⊥) ↦ refl_{rec⊥} ]〉)
--       → ((π₁ (((((((r I) (λt → (psi t) ∧ (phi t))) (λt → ⊥)) (λt → A t)) (λt → rec⊥)) (λt → a_psi t)) (λt → a_phi t))) (g₁ y)) =_{〈{t : I | (psi t) ∧ (phi t)} → (a_psi t) =_{A t} (a_phi t)[ (((psi t) ∧ (phi t)) ∧ ⊥) ↦ refl_{rec⊥} ]〉} y
